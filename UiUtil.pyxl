// Scripts, variables, and constants here are visible to all modes
let curr_drag = ∅
let drag_ended = ∅
let cursor_hover = xy(0, 0)
let mouse_moved = false

def update_cursor_hover():
    const mouse = device_control("get_mouse_state")
    if mouse.dx or mouse.dy:
        cursor_hover = mouse.xy
        mouse_moved = true
        

def update_curr_drag():
    drag_ended = ∅

    if touch.pressed_a and not curr_drag:
        curr_drag = {
            start_frame: mode_frames,
            start_pos: touch.xy,
        }
    
    if touch.released_a:
        drag_ended = curr_drag
        curr_drag = ∅
    
    
def is_click():
    if not (curr_drag and curr_drag.start_frame ≥ mode_frames - FRAMES_LONG_PRESS):
        return false
    
    let release_pos = ∅
    if touch.released_a:
        release_pos = touch.xy
        
    return release_pos and ‖release_pos - curr_drag.start_pos‖ < TOUCH_CLICK_RADIUS


def is_long_press():
    return curr_drag and curr_drag.start_frame < mode_frames - FRAMES_LONG_PRESS
    
def long_press_ended():
    return drag_ended
    
def draw_mouse_cursor():
    if not mouse_moved:
        return 
    
    const sprite = Cursor_sprite
    const sprite_offset = ½ sprite.size
    draw_sprite({
        pos: cursor_hover + sprite_offset + xy(0, 1),
        sprite: sprite,
        z: Z_CURSOR,
        override_color: #000,
    })
    draw_sprite({
        pos: cursor_hover + sprite_offset,
        sprite: sprite,
        z: Z_CURSOR,
    })
    
def is_in_corner_box(pos, corner_box):
    const tl = corner_box.corner
    const br = corner_box.corner + corner_box.size
    return (is_between(pos.x, tl.x, br.x)
        and is_between(pos.y, tl.y, br.y))

// draws a button.
// returns true if the button was clicked
def button_clicked(args):
    const state = button_state(args)
    return state.clicked
    
// Draws a button.
// returns a dict with:
// .hover   - true if mouse is over
// .active  - true if currently pressed
// .clicked - true if "clicked" this frame
// .text    - if drawing an icon, this is the text that goes along with it
def button_state(args):
    const button_font = font
    const disabled = args.disabled
    const hover = is_in_corner_box(cursor_hover, args)
    const long_press = is_long_press()
    const long_press_ended = long_press and touch.released_a
    const long_press_started_here = (long_press and is_in_corner_box(curr_drag.start_pos, args))
    const active = not disabled and ((hover and touch.a) or long_press_started_here)
    
    preserving_transform:
        if active:
            compose_transform({
                pos: xy(0, 1),
            })
        
        let button_outline = (if args.outline 
                              then args.outline 
                              else BUTTON_OUTLINE_COLOR)

        let fill_color = BUTTON_FILL_COLOR
        if disabled:
            fill_color = BUTTON_FILL_DISABLED_COLOR
        if hover: 
            fill_color = perceptual_lerp_color(fill_color, rgba(1, 1, 1, fill_color.a), 5%)
        draw_corner_rect({
            corner: args.corner,
            size: args.size,
            color: fill_color,
            outline: button_outline,
        })

        let text_color = BUTTON_TEXT_COLOR
        if disabled:
            text_color = BUTTON_TEXT_COLOR

        const sprite = args.sprite
        if sprite:
            const sprite_scale = 1
            const sprite_pos = args.corner + xy(½ args.size.x, ½ args.sprite.size.y + 3)
            draw_sprite({
                sprite: sprite,
                pos: sprite_pos + xy(0, 1),
                scale: sprite_scale,
                override_color: button_outline,
            })
            draw_sprite({
                sprite: sprite,
                pos: sprite_pos,
                scale: sprite_scale,
                override_color: text_color,
            })
            draw_text({
                font: button_font,
                text: args.text,
                pos: args.corner + xy(½,1)*args.size + xy(0, -4),
                color: text_color,
                shadow: COLORS.menu_highlight,
                outline: if disabled then nil else button_outline,
                x_align: "center",
                y_align: "bottom",
            })
        else:
            const text_pos = args.corner + ½ args.size
            draw_text({
                font: font,
                text: args.text,
                pos: text_pos,
                color: text_color,
                shadow: COLORS.menu_highlight,
                outline: if disabled then nil else button_outline,
                x_align: "center",
                y_align: "center",
            })
    
    return {
        hover: hover,
        clicked: hover and (is_click() or (long_press_ended and long_press_started_here)),
        active: active,
        text: args.text,
        alt: args.alt,
    }