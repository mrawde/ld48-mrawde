
def init_colors_from_sprite(row):
    let colors = []
    const spritesheet = TeamColors_sprite
    for i < spritesheet.size.x:
        push(colors, get_sprite_pixel_color(spritesheet[i][row], xy(0, 0)))
    return colors
const COLORS = init_colors_from_sprite(1)
const SECONDARY_COLORS = init_colors_from_sprite(0)
const HUD_LEVEL_FONT = weaver_10_font
const TEXT_FONT = deja_8_font

const GROUND_COLOR = get_sprite_pixel_color(GroundTiles_sprite[0][0], xy(2, 1))

def draw_game(level_elem, player_state):
    draw_level(level_elem, player_state)
    draw_hud(game_progress)
    
    preserving_transform:
        compose_transform({
            pos: compute_offset_for_board(level_elem.state.size),
        })
        draw_path(player_state.path, SECONDARY_COLORS[player_state.curr_truck])
 
def compute_offset_for_board(board_size):
    const offset_for_size = ½ (6 - board_size.x) * TILE_SIZE
    return BOARD_OFFSET + offset_for_size
    
def draw_level(level_elem, player_state) preserving_transform:
    const level_state = level_elem.state
    compose_transform({
        pos: compute_offset_for_board(level_state.size),
    })
    
    const padding = ⅛ TILE_SIZE
    draw_corner_rect({
        corner: -padding,
        size: TILE_SIZE * level_state.size + 2*padding,
        color: BACKGROUND_COLOR,
    })
        
    let tile_rand = make_random(level_state.num)

    for y < level_state.size.y:
        for x < level_state.size.x:
            const coord = xy(x, y)
            const i = to_index(level_state.size, coord)
            const corner = xy(x, y) * TILE_SIZE
            
            const spritesheet_x = random_integer(0, 3, tile_rand)
            const spritesheet_y = if level_state.visited[i] then 1 else 0
            const sprite = GroundTiles_sprite[spritesheet_x][spritesheet_y]
            const sprite_pos = corner + ½ TILE_SIZE
            draw_sprite({
                sprite: sprite,
                pos: sprite_pos,
                z: Z_TILE,
            })
            
    const goals = level_state.goals
    for i < size(goals):
        const goal = goals[i]
        draw_goal(i, goal)    
        
    const trucks = level_elem.trucks
    for i < size(trucks):
        const truck_elem = trucks[i]
        const truck_color = COLORS[i]
        const is_current = i == player_state.curr_truck
        draw_truck(truck_elem, truck_color, is_current)

        
def draw_truck(truck_elem, truck_color, is_current):
    const frame_t = mode_frames / 30

    const jitter = if is_current then xy(oscillate(mode_frames * 0.5, -0.5, 0.5), 0) else xy(0, 0)
    const offset = truck_elem.offset + jitter
    const angle = truck_elem.angle
    const corner = truck_elem.state.coord * TILE_SIZE + offset
    const anim = truck_elem.anim
    const sprite = Truck_sprite[anim].frame(frame_t)
    const sprite_pos = corner + ½ TILE_SIZE
    
    if is_current:
        for o in [xy(1, 0), xy(-1, 0), xy(0, 1), xy(0, -1)]:
            draw_sprite({
                sprite:sprite,
                pos: sprite_pos + o,
                override_color: SELECT_OUTLINE_COLOR,
                angle: angle,
                z: Z_TRUCK,
            })
    draw_sprite({
        sprite: sprite,
        pos: sprite_pos,
        angle: angle,
        z: Z_TRUCK,
    })
    
    const which_truck = truck_elem.which
    const color = COLORS[which_truck]
    const outline = SECONDARY_COLORS[which_truck]
    draw_disk({
        pos: sprite_pos,
        radius:TRUCK_DOT_RADIUS,
        color: color,
        outline: outline,
        z: Z_TRUCK,
    })
    
def draw_goal(truck_idx, goal):
    const corner = goal.coord * TILE_SIZE
    const truck_color = COLORS[truck_idx]
    const sec_color = SECONDARY_COLORS[truck_idx]
    const z = Z_TILE

    const spritesheet_x = truck_idx mod size(GroundTiles_sprite)
    const sprite = GroundTiles_sprite[spritesheet_x][2]
    const sprite_pos = corner + ½ TILE_SIZE
    draw_sprite({
        sprite: sprite,
        pos: sprite_pos,
        z: z,
    })
    
    // draw the platform as 4 rectangles
    const r_size = ½ TILE_SIZE
    for ry < 2:
        for rx < 2:
            const r_pos = (xy(0.5, 0.5) + xy(rx, ry)) * r_size
            draw_rect({
                pos: corner + r_pos,
                size: r_size - 2,
                outline: sec_color,
                z: z,
            })    
            
    draw_disk({
        pos: sprite_pos,
        radius: TRUCK_DOT_RADIUS,
        color: truck_color,
        outline: sec_color,
        z: z,
    })

def draw_hud(game_progress):
    const pack = PACKS[game_progress.curr_pack_idx]
    const level = pack.levels[game_progress.curr_level_idx]
    const level_num = level.num
    draw_text({
        pos: HUD_LEVEL_OFFSET,
        font: HUD_LEVEL_FONT,
        text: replace("PACK\nN of TOTAL", {
            "PACK": pack.name,
            "N": level_num,
            "TOTAL": size(pack.levels),
        }),
        color: HUD_LEVEL_COLOR,
    })
    
    draw_text({
        pos: TEXT_BOX_OFFSET,
        font: TEXT_FONT,
        text: level.text,
        wrap_width: TEXT_BOX_WIDTH,
        color: HUD_TEXT_COLOR,
    })
        
def draw_path(path, color) preserving_transform:
    if size(path) < 1:
        return
    
    const z = Z_PATH
    compose_transform({
        pos: ½ TILE_SIZE,
        z: z,
    })
    
    let last = path[0]

    const width = 10
    const radius = if width > 2 then 0.5 * width else 0
    if radius:
        draw_disk({
            pos: last * TILE_SIZE,
            color: color,
            radius: radius,
        })

    for 0 < i < size(path):
        let curr = path[i]
        if radius:
            draw_disk({
                pos: curr * TILE_SIZE,
                color: color,
                radius: radius,
            })
        draw_line({
            A: last * TILE_SIZE,
            B: curr * TILE_SIZE,
            color: color,
            width: width,
        })
        last = curr

